plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group 'de.alarmclock'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task cleanAndRun {
    dependsOn 'clean'
    dependsOn 'run'
    tasks.findByName('run').mustRunAfter 'clean'
}

application {
    mainModule = "${group}.soundpicker"
    mainClass = "${group}.soundpicker.Main"
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': application.mainClassName
        )
    }
    from { compileJava.classpath.collect { it.isDirectory() ? it : zipTree(it) } }
}